apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

sourceSets {
    functionalTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

group = 'com.banyaibalazs.stubber'
version = '1.0.0'

publishing {
    repositories {
        maven {
            name 'bintray'
            url "https://api.bintray.com/maven/balazsbanyai/stubber/stubber/;publish=1"
            credentials {
                username System.getenv('BINTRAY_USER')
                password System.getenv('BINTRAY_KEY')
            }
        }
    }
}


configurations {
    functionalTestCompile.extendsFrom testCompile
    functionalTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile gradleApi()
    compile group: 'javassist', name: 'javassist', version: '3.12.1.GA'
    testCompile gradleTestKit()
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

task functionalTest(type: Test) { task ->
    group 'verification'
    description 'Runs the functional tests.'
    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath
    tasks['check'].dependsOn task
    reports.html.destination file("$reportsDir/functionalTests")
}